#include <esp_task_wdt.h>
#include "pitches.h"
#include <Adafruit_NeoPixel.h>
#include <Wire.h>
#include "SSD1306Ascii.h"
#include "SSD1306AsciiWire.h"
#define SR04_I2CADDR 0x57
#define OLED_I2CAADR 0x3C
#define I2CADDR 0x13

#define I2C_SDA 19
#define I2C_SCL 18
#define LED 2

#define NEO_PIXEL 5
#define LED_COUNT 3

Adafruit_NeoPixel strip(LED_COUNT, NEO_PIXEL, NEO_GRB + NEO_KHZ800);
SSD1306AsciiWire oled;

byte attinySlaveArray[3];
unsigned int safeDistance = 100;
int currentDistance = 200;


int setFreq(unsigned int frequency) {
    attinySlaveArray[0] = 0x10;  // Command 0x10
    attinySlaveArray[1] = (frequency & 0xff);        
    attinySlaveArray[2] = (frequency >> 8) & 0xff;  
    delay(10);
    Wire.beginTransmission(I2CADDR);
    Wire.write(attinySlaveArray, 3); 
    if (Wire.endTransmission () == 0) { 
        Serial.println("i2c Write to 0x12 Sucessfull");
        return 0;
    }
    else {
        Serial.println("i2c Write Failed");
        return 1;
    }
}


int setToneDuration(unsigned int duration) {
    attinySlaveArray[0] = 0x11;  
    attinySlaveArray[1] = (duration & 0xff);        
    attinySlaveArray[2] = (duration >> 8) & 0xff;   
    delay(10);
    Wire.beginTransmission(I2CADDR);
    Wire.write(attinySlaveArray, 3); 
    if (Wire.endTransmission () == 0) {  
        Serial.println("i2c Write to 0x12 Sucessfull");
        return 0;
    }
    else {
        Serial.println("i2c Write Failed");
        return 1;
    }
}


void playNote(unsigned int frequency, unsigned int duration) {
  setFreq(frequency);
  setToneDuration(duration * 0.9);
  delay(duration); 
}



int ping_mm()
{
    unsigned long distance = 0;
    byte i;   
    byte ds[3];
    long timeChecked;
    
    Wire.beginTransmission(SR04_I2CADDR);
    Wire.write(1);          
    Wire.endTransmission();

    vTaskDelay(pdMS_TO_TICKS(200)); 

    i = 0;
    Wire.requestFrom(0x57,3);         
    while (Wire.available())
    {
     ds[i++] = Wire.read();
    }        
    
    distance = (unsigned long)(ds[0] << 16);
    distance = distance + (unsigned long)(ds[1] << 8);
    distance = (distance + (unsigned long)(ds[2])) / 1000;
    if ((10 <= distance) && (6000 >= distance)) {
        return (int)distance;
    }
    else {
        return -1;
    }
}

void setup() {
    Wire.begin(I2C_SDA, I2C_SCL);    
    oled.begin(&Adafruit128x64, OLED_I2CAADR); 
    pinMode(LED, OUTPUT);
    delay(2000);
    
    Serial.begin(115200);
    
    oled.setFont(Adafruit5x7);
    oled.clear();
    oled.println("Proximity Sentry Alarm!");

    strip.begin();          
    strip.setBrightness(50); 
    randomSeed(analogRead(0));
    
}


void loop() {

  
          
        currentDistance = ping_mm();
        
        if (currentDistance > 0) { 
              oled.clear();
              oled.println("Range <->");
              oled.print("Distance(mm): "); 
              oled.println(currentDistance);    
          
              if(currentDistance < safeDistance) {
                  oled.println("");
                  oled.println("Danger ahead!");
                  
                  strip.setPixelColor(0, strip.Color(random(255),0,0));  
                  strip.setPixelColor(1, strip.Color(random(255),0,0));  
                  strip.setPixelColor(2, strip.Color(random(255),0,0));  
                  strip.show(); 
              
                  playNote(NOTE_A4, 100);
                  playNote(NOTE_A4, 100);
                  playNote(NOTE_C4, 100);           
          
              }
              else {          
                  strip.setPixelColor(0, strip.Color(0,0, (currentDistance < (safeDistance * 1.5)) ? 255 : 0 ));
                  strip.setPixelColor(1, strip.Color(0,0, (currentDistance < (safeDistance * 3)) ? 255 : 0 ));
                  strip.setPixelColor(2, strip.Color(0,0, (currentDistance < (safeDistance * 6)) ? 255 : 0 ));
                  strip.show(); // This sends the updated pixel color to the hardware.
              }          
        } 
        else {
            oled.clear();
            oled.println("Range <->");
            oled.print("Out-of-Range!!");
            
            strip.setPixelColor(0, strip.Color(0,0,0));    
            strip.setPixelColor(1, strip.Color(0,0,0));       
            strip.setPixelColor(2, strip.Color(0,0,0));       
            strip.show(); 
        }
}
